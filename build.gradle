buildscript {
  repositories {
    mavenCentral()
    maven {
      url "https://jitpack.io"
    }
  }
  dependencies {
    classpath "com.cyclonedx:cyclonedx-gradle-plugin:1.2.0"
  }
}

plugins {
  id 'org.springframework.boot' version '2.1.7.RELEASE'
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'
  id 'java'
  id 'idea'
  id "com.github.spotbugs" version "5.0.7"
  id "org.sonarqube" version "3.3"
  id "org.owasp.dependencycheck" version "6.3.1"
}

apply plugin: "org.cyclonedx.bom"

group = 'tw'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11' //可根据你的机器环境，调整为对应的JDK版本，例如”8“

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  spotbugs 'com.github.spotbugs:spotbugs:4.7.0'
  spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'
}

test {
  useJUnitPlatform()
}

spotbugsMain {
  reports {
    html {
      enabled = true
      destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
      stylesheet = 'fancy-hist.xsl'
    }
  }
}

spotbugs {
  ignoreFailures = false
  showStackTraces = true
  showProgress = true
  effort = 'max'
  reportLevel = 'low'
  reportsDir = file("$buildDir/spotbugs")
  maxHeapSize = '1g'
  extraArgs = [ '-nested:false' ]
  jvmArgs = [ '-Duser.language=en' ]
}

// SonarScanner for Gradle
sonarqube {
  properties {
    property "sonar.sourceEncoding", "UTF-8"
    property "sonar.sources", "src"
  }
}
